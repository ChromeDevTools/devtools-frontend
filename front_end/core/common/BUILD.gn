# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/typescript/typescript.gni")

ts_library("ColorUtils") {
  sources = [ "ColorUtils.ts" ]
}

devtools_module("common") {
  sources = [
    "App.ts",
    "AppProvider.ts",
    "Base64.ts",
    "CharacterIdMap.ts",
    "Color.ts",
    "ColorConverter.ts",
    "Console.ts",
    "Debouncer.ts",
    "EventTarget.ts",
    "JavaScriptMetaData.ts",
    "Lazy.ts",
    "Linkifier.ts",
    "Mutex.ts",
    "Object.ts",
    "ParsedURL.ts",
    "Progress.ts",
    "QueryParamHandler.ts",
    "ResolverBase.ts",
    "ResourceType.ts",
    "Revealer.ts",
    "Runnable.ts",
    "SegmentedRange.ts",
    "SettingRegistration.ts",
    "Settings.ts",
    "SimpleHistoryManager.ts",
    "StringOutputStream.ts",
    "TextDictionary.ts",
    "Throttler.ts",
    "Trie.ts",
    "Worker.ts",
  ]

  deps = [
    ":ColorUtils",
    "../../core/i18n:bundle",
    "../../core/platform:bundle",
    "../../core/root:bundle",
  ]

  # Add files built by ColorUtils ts_library so that they are included in layout tests.
  data = filter_include(get_target_outputs(":ColorUtils"),
                        [
                          "*.js",
                          "*.map",
                        ])
}

devtools_entrypoint("bundle") {
  entrypoint = "common.ts"

  deps = [
    ":ColorUtils",
    ":common",
  ]

  visibility = [ "*" ]
}

ts_library("unittests") {
  testonly = true

  sources = [
    "Base64.test.ts",
    "CharacterIdMap.test.ts",
    "Color.test.ts",
    "ColorConverter.test.ts",
    "ColorUtils.test.ts",
    "Console.test.ts",
    "Debouncer.test.ts",
    "EventTarget.test.ts",
    "Lazy.test.ts",
    "Mutex.test.ts",
    "Object.test.ts",
    "ParsedURL.test.ts",
    "Progress.test.ts",
    "ResolverBase.test.ts",
    "ResourceType.test.ts",
    "Revealer.test.ts",
    "SegmentedRange.test.ts",
    "SettingRegistration.test.ts",
    "Settings.test.ts",
    "SimpleHistoryManager.test.ts",
    "StringOutputStream.test.ts",
    "TextDictionary.test.ts",
    "Throttler.test.ts",
    "Trie.test.ts",
  ]

  deps = [
    ":bundle",
    "../../testing",
    "../../ui/legacy/components/quick_open:bundle",
    "../i18n:bundle",
  ]
}
