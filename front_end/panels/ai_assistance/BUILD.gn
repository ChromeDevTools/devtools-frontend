# Copyright 2024 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")
import("../../../scripts/build/typescript/typescript.gni")
import("../visibility.gni")

generate_css("css_files") {
  sources = [
    "./aiAssistancePanel.css",
    "./components/chatView.css",
    "./components/exploreWidget.css",
    "./components/userActionRow.css",
    "./selectWorkspaceDialog.css",
  ]
}

devtools_module("ai_assistance") {
  sources = [
    "AiAssistancePanel.ts",
    "PatchWidget.ts",
    "SelectWorkspaceDialog.ts",
    "components/ChatView.ts",
    "components/ExploreWidget.ts",
    "components/MarkdownRendererWithCodeBlock.ts",
    "components/UserActionRow.ts",
  ]

  deps = [
    ":css_files",
    "../../core/common:bundle",
    "../../core/host:bundle",
    "../../core/i18n:bundle",
    "../../core/root:bundle",
    "../../models/ai_assistance:bundle",
    "../../models/persistence:bundle",
    "../../models/workspace:bundle",
    "../../models/workspace_diff:bundle",
    "../../panels/changes:bundle",
    "../../panels/common:bundle",
    "../../panels/elements:bundle",
    "../../panels/network:bundle",
    "../../panels/sources:bundle",
    "../../panels/timeline:bundle",
    "../../panels/timeline/utils:bundle",
    "../../third_party/marked:bundle",
    "../../ui/components/markdown_view:bundle",
    "../../ui/components/snackbars:bundle",
    "../../ui/components/spinners:bundle",
    "../../ui/legacy:bundle",
    "../../ui/lit:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "ai_assistance.ts"

  deps = [ ":ai_assistance" ]

  visibility = [
    ":*",
    "../../../test/interactions/*",
    "../../entrypoints/*",
    "../../ui/components/docs/ai_assistance/*",
    "../explain",
  ]

  visibility += devtools_panels_visibility
}

devtools_entrypoint("meta") {
  entrypoint = "ai_assistance-meta.ts"

  deps = [ ":bundle" ]

  visibility = [ "../../entrypoints/*" ]
}

ts_library("unittests") {
  testonly = true

  sources = [
    "AiAssistancePanel.test.ts",
    "PatchWidget.test.ts",
    "SelectWorkspaceDialog.test.ts",
    "components/ChatView.test.ts",
    "components/ExploreWidget.test.ts",
    "components/MarkdownRendererWithCodeBlock.test.ts",
    "components/UserActionRow.test.ts",
  ]

  deps = [
    ":bundle",
    "../../core/common:bundle",
    "../../core/host:bundle",
    "../../core/platform:bundle",
    "../../core/sdk:bundle",
    "../../generated:protocol",
    "../../panels/timeline/utils:bundle",
    "../../testing",
    "../../ui/legacy:bundle",
  ]
}
