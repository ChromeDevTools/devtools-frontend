# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")
import("../../../scripts/build/typescript/typescript.gni")
import("../visibility.gni")

generate_css("css_files") {
  sources = [
    "lighthouseDialog.css",
    "lighthousePanel.css",
    "lighthouseStartView.css",
  ]
}

devtools_module("lighthouse") {
  sources = [
    "LighthouseController.ts",
    "LighthousePanel.ts",
    "LighthouseProtocolService.ts",
    "LighthouseReportRenderer.ts",
    "LighthouseReportSelector.ts",
    "LighthouseReporterTypes.ts",
    "LighthouseStartView.ts",
    "LighthouseStatusView.ts",
    "LighthouseTimespanView.ts",
    "RadioSetting.ts",
  ]

  deps = [
    "../../core/common:bundle",
    "../../core/host:bundle",
    "../../core/i18n:bundle",
    "../../core/platform:bundle",
    "../../core/protocol_client:bundle",
    "../../core/root:bundle",
    "../../core/sdk:bundle",
    "../../models/trace:bundle",
    "../../models/workspace:bundle",
    "../../panels/emulation:bundle",
    "../../third_party/lighthouse/report:report",
    "../../ui/legacy:bundle",
    "../../ui/legacy/components/utils:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "lighthouse.ts"

  deps = [
    ":css_files",
    ":lighthouse",
    "../../third_party/lighthouse",
  ]

  visibility = [
    ":*",
    "../../entrypoints/*",
  ]

  visibility += devtools_panels_visibility
}

devtools_entrypoint("meta") {
  entrypoint = "lighthouse-meta.ts"

  deps = [
    ":bundle",
    "../../core/i18n:bundle",
    "../../ui/legacy:bundle",
  ]

  visibility = [ "../../entrypoints/*" ]
}

ts_library("unittests") {
  testonly = true

  sources = [
    "LighthouseController.test.ts",
    "LighthousePanel.test.ts",
    "LighthouseProtocolService.test.ts",
    "LighthouseReportRenderer.test.ts",
  ]

  deps = [
    ":bundle",
    "../../testing",
  ]
}
