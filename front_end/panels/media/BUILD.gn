# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")
import("../../../scripts/build/typescript/typescript.gni")
import("../visibility.gni")

generate_css("css_files") {
  sources = [
    "eventDisplayTable.css",
    "playerListView.css",
    "playerMessagesView.css",
    "playerPropertiesView.css",
  ]
}

devtools_module("media") {
  sources = [
    "EventDisplayTable.ts",
    "EventTimelineView.ts",
    "MainView.ts",
    "MediaModel.ts",
    "PlayerDetailView.ts",
    "PlayerListView.ts",
    "PlayerMessagesView.ts",
    "PlayerPropertiesView.ts",
    "TickingFlameChart.ts",
    "TickingFlameChartHelpers.ts",
  ]

  deps = [
    "../../core/common:bundle",
    "../../core/host:bundle",
    "../../core/i18n:bundle",
    "../../core/sdk:bundle",
    "../../generated:protocol",
    "../../ui/legacy:bundle",
    "../../ui/legacy/components/data_grid:bundle",
    "../../ui/legacy/components/perf_ui:bundle",
    "../../ui/legacy/theme_support:bundle",
    "../../ui/visual_logging:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "media.ts"

  deps = [
    ":css_files",
    ":media",
  ]

  visibility = [
    ":*",
    "../../entrypoints/*",
  ]

  visibility += devtools_panels_visibility
}

devtools_entrypoint("meta") {
  entrypoint = "media-meta.ts"

  deps = [
    ":bundle",
    "../../core/i18n:bundle",
    "../../ui/legacy:bundle",
  ]

  visibility = [ "../../entrypoints/*" ]
}

ts_library("unittests") {
  testonly = true

  sources = [
    "MainView.test.ts",
    "TickingFlameChartHelpers.test.ts",
  ]

  deps = [
    ":bundle",
    "../../core/sdk:bundle",
    "../../testing",
  ]
}
