# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")
import("../../../third_party/typescript/typescript.gni")
import("../visibility.gni")

generate_css("css_files") {
  sources = [ "performanceMonitor.css" ]
}

devtools_module("performance_monitor") {
  sources = [ "PerformanceMonitor.ts" ]

  deps = [
    "../../core/common:bundle",
    "../../core/host:bundle",
    "../../core/i18n:bundle",
    "../../core/platform:bundle",
    "../../core/sdk:bundle",
    "../../ui/components/icon_button:bundle",
    "../../ui/legacy:bundle",
    "../../ui/legacy/theme_support:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "performance_monitor.ts"

  deps = [
    ":css_files",
    ":performance_monitor",
  ]

  visibility = [
    ":*",
    "../../entrypoints/*",
  ]

  visibility += devtools_panels_visibility
}

devtools_entrypoint("meta") {
  entrypoint = "performance_monitor-meta.ts"

  deps = [
    ":bundle",
    "../../core/i18n:bundle",
    "../../ui/legacy:bundle",
  ]

  visibility = [ "../../entrypoints/*" ]
}

ts_library("unittests") {
  testonly = true

  sources = [ "PerformanceMonitor.test.ts" ]

  deps = [
    ":bundle",
    "../../testing",
  ]
}
