# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")
import("../../../scripts/build/typescript/typescript.gni")
import("../visibility.gni")

generate_css("css_files") {
  sources = [ "screencastView.css" ]
}

devtools_module("screencast") {
  sources = [
    "InputModel.ts",
    "ScreencastApp.ts",
    "ScreencastView.ts",
  ]

  deps = [
    "../../core/common:bundle",
    "../../core/host:bundle",
    "../../core/i18n:bundle",
    "../../core/sdk:bundle",
    "../../generated:protocol",
    "../../ui/components/icon_button:bundle",
    "../../ui/legacy:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "screencast.ts"

  deps = [
    ":css_files",
    ":screencast",
  ]

  visibility = [
    ":*",
    "../../entrypoints/*",
  ]

  visibility += devtools_panels_visibility
}

devtools_entrypoint("meta") {
  entrypoint = "screencast-meta.ts"

  deps = [
    ":bundle",
    "../../core/common:bundle",
    "../../ui/legacy:bundle",
  ]

  visibility = [ "../../entrypoints/*" ]
}

ts_library("unittests") {
  testonly = true

  sources = [ "ScreencastApp.test.ts" ]

  deps = [
    ":bundle",
    "../../testing",
  ]
}
