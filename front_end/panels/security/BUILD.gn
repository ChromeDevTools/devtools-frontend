# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")
import("../../../scripts/build/typescript/typescript.gni")
import("../visibility.gni")

generate_css("css_files") {
  sources = [
    "cookieControlsView.css",
    "cookieReportView.css",
    "lockIcon.css",
    "mainView.css",
    "originView.css",
    "sidebar.css",
  ]
}

devtools_module("security") {
  sources = [
    "CookieControlsTreeElement.ts",
    "CookieControlsView.ts",
    "CookieReportTreeElement.ts",
    "CookieReportView.ts",
    "OriginTreeElement.ts",
    "SecurityModel.ts",
    "SecurityPanel.ts",
    "SecurityPanelSidebar.ts",
    "SecurityPanelSidebarTreeElement.ts",
  ]

  deps = [
    "../../core/common:bundle",
    "../../core/host:bundle",
    "../../core/i18n:bundle",
    "../../core/root:bundle",
    "../../core/sdk:bundle",
    "../../generated:protocol",
    "../../models/issues_manager:bundle",
    "../../panels/network/forward:bundle",
    "../../ui/components/buttons:bundle",
    "../../ui/components/cards:bundle",
    "../../ui/components/chrome_link:bundle",
    "../../ui/components/switch:bundle",
    "../../ui/legacy:bundle",
    "../../ui/legacy/components/data_grid:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "security.ts"

  deps = [
    ":css_files",
    ":security",
  ]

  visibility = [
    ":*",
    "../../entrypoints/*",
    "../console/*",
  ]

  visibility += devtools_panels_visibility
}

devtools_entrypoint("meta") {
  entrypoint = "security-meta.ts"

  deps = [
    ":bundle",
    "../../core/common:bundle",
    "../../core/i18n:bundle",
    "../../core/root:bundle",
    "../../ui/legacy:bundle",
  ]

  visibility = [ "../../entrypoints/*" ]
}

ts_library("unittests") {
  testonly = true

  sources = [
    "CookieControlsView.test.ts",
    "CookieReportView.test.ts",
    "SecurityModel.test.ts",
    "SecurityPanel.test.ts",
  ]

  deps = [
    ":bundle",
    "../../core/platform:bundle",
    "../../core/root:bundle",
    "../../core/sdk:bundle",
    "../../generated:protocol",
    "../../testing",
  ]
}
