# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")
import("../../../scripts/build/typescript/typescript.gni")
import("../visibility.gni")

generate_css("css_files") {
  sources = [
    "aiSettingsTab.css",
    "frameworkIgnoreListSettingsTab.css",
    "keybindsSettingsTab.css",
    "settingsScreen.css",
    "workspaceSettingsTab.css",
  ]
}

devtools_module("settings") {
  sources = [
    "AISettingsTab.ts",
    "FrameworkIgnoreListSettingsTab.ts",
    "KeybindsSettingsTab.ts",
    "SettingsScreen.ts",
    "WorkspaceSettingsTab.ts",
  ]

  deps = [
    "../../core/common:bundle",
    "../../core/host:bundle",
    "../../core/i18n:bundle",
    "../../core/platform:bundle",
    "../../core/root:bundle",
    "../../core/sdk:bundle",
    "../../generated:protocol",
    "../../models/ai_assistance:bundle",
    "../../models/bindings:bundle",
    "../../models/breakpoints:bundle",
    "../../models/persistence:bundle",
    "../../models/project_settings:bundle",
    "../../models/text_utils:bundle",
    "../../models/workspace:bundle",
    "../../ui/components/cards:bundle",
    "../../ui/components/icon_button:bundle",
    "../../ui/components/legacy_wrapper:bundle",
    "../../ui/components/switch:bundle",
    "../../ui/legacy:bundle",
    "../../ui/legacy/components/data_grid:bundle",
    "../../ui/legacy/components/utils:bundle",
    "../../ui/lit:bundle",
    "../utils:bundle",
    "./components:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "settings.ts"

  deps = [
    ":css_files",
    ":settings",
  ]

  visibility = [
    ":*",
    "../../entrypoints/*",
    "../../panels/profiler/*",
  ]

  visibility += devtools_panels_visibility
}

devtools_entrypoint("meta") {
  entrypoint = "settings-meta.ts"

  deps = [
    ":bundle",
    "../../core/common:bundle",
    "../../core/i18n:bundle",
    "../../core/root:bundle",
    "../../ui/legacy:bundle",
    "./emulation:meta",
  ]

  visibility = [ "../../entrypoints/*" ]
}

ts_library("unittests") {
  testonly = true

  sources = [
    "AISettingsTab.test.ts",
    "WorkspaceSettingsTab.test.ts",
  ]

  deps = [
    ":bundle",
    "../../core/common:bundle",
    "../../testing",
  ]
}
