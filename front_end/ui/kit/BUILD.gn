# Copyright 2025 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/component_doc.gni")
import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")

generate_css("css_files") {
  sources = [
    "cards/card.css",
    "icons/icon.css",
    "link/link.css",
  ]
}

devtools_ui_module("kit") {
  sources = [
    "cards/Card.ts",
    "icons/Icon.ts",
    "link/Link.ts",
  ]

  deps = [
    "../../Images:optimize_images",
    "../../core/platform:bundle",
    "../helpers:bundle",
    "../lit:bundle",
    "../visual_logging:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "kit.ts"

  deps = [
    ":css_files",
    ":kit",
  ]

  visibility = [
    ":*",
    "../*",
    "../../entrypoints/main/*",
    "../../panels/*",
  ]
}

devtools_ui_module("unittests") {
  testonly = true

  sources = [
    "cards/Card.test.ts",
    "icons/Icon.test.ts",
    "link/Link.test.ts",
  ]

  deps = [
    ":bundle",
    "../../testing",
  ]
}

component_doc("docs") {
  sources = [
    "cards/Card.docs.ts",
    "icons/Icon.docs.ts",
    "link/Link.docs.ts",
  ]
  deps = [
    ":bundle",
    "../lit:bundle",
  ]
}
