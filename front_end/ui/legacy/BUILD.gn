# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../scripts/build/ninja/component_doc.gni")
import("../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../scripts/build/ninja/devtools_module.gni")
import("../../../scripts/build/ninja/generate_css.gni")
import("../../../scripts/build/typescript/typescript.gni")
import("./visibility.gni")

generate_css("css_files") {
  sources = [
    "checkboxTextLabel.css",
    "confirmDialog.css",
    "dialog.css",
    "dropTarget.css",
    "emptyWidget.css",
    "filter.css",
    "floaty.css",
    "glassPane.css",
    "infobar.css",
    "inspectorCommon.css",
    "listWidget.css",
    "popover.css",
    "progressIndicator.css",
    "remoteDebuggingTerminatedScreen.css",
    "reportView.css",
    "rootView.css",
    "searchableView.css",
    "smallBubble.css",
    "softContextMenu.css",
    "softDropDown.css",
    "softDropDownButton.css",
    "splitWidget.css",
    "suggestBox.css",
    "tabbedPane.css",
    "targetCrashedScreen.css",
    "textPrompt.css",
    "toolbar.css",
    "treeoutline.css",
    "viewContainers.css",
  ]
}

copy_to_gen("copy_stylesheets_for_server") {
  sources = [ "inspectorCommon.css" ]
}

devtools_ui_module("ui") {
  sources = [
    "ARIAUtils.ts",
    "ActionRegistration.ts",
    "ActionRegistry.ts",
    "Context.ts",
    "ContextFlavorListener.ts",
    "ContextMenu.ts",
    "DOMUtilities.ts",
    "Dialog.ts",
    "DockController.ts",
    "DropTarget.ts",
    "EmptyWidget.ts",
    "FilterBar.ts",
    "FilterSuggestionBuilder.ts",
    "Floaty.ts",
    "ForwardedInputEventHandler.ts",
    "Fragment.ts",
    "GlassPane.ts",
    "Infobar.ts",
    "InplaceEditor.ts",
    "InspectorView.ts",
    "KeyboardShortcut.ts",
    "ListControl.ts",
    "ListModel.ts",
    "ListWidget.ts",
    "Panel.ts",
    "PopoverHelper.ts",
    "ProgressIndicator.ts",
    "RemoteDebuggingTerminatedScreen.ts",
    "ReportView.ts",
    "ResizerWidget.ts",
    "RootView.ts",
    "SearchableView.ts",
    "ShortcutRegistry.ts",
    "SoftContextMenu.ts",
    "SoftDropDown.ts",
    "SplitWidget.ts",
    "SuggestBox.ts",
    "TabbedPane.ts",
    "TargetCrashedScreen.ts",
    "TextPrompt.ts",
    "Toolbar.ts",
    "Tooltip.ts",
    "Treeoutline.ts",
    "UIUtils.ts",
    "View.ts",
    "ViewManager.ts",
    "ViewRegistration.ts",
    "Widget.ts",
    "XElement.ts",
    "XLink.ts",
    "ZoomManager.ts",
  ]

  deps = [
    "../../core/common:bundle",
    "../../core/dom_extension:bundle",
    "../../core/host:bundle",
    "../../core/i18n:bundle",
    "../../core/platform:bundle",
    "../../core/root:bundle",
    "../../core/sdk:bundle",
    "../../foundation:bundle",
    "../../generated:protocol",
    "../../models/annotations:bundle",
    "../../models/geometry:bundle",
    "../../models/greendev:bundle",
    "../../models/logs:bundle",
    "../../models/text_utils:bundle",
    "../../models/trace:bundle",
    "../../ui/legacy/theme_support:bundle",
    "../components/buttons:bundle",
    "../components/helpers:bundle",
    "../components/highlighting:bundle",
    "../helpers:bundle",
    "../kit:bundle",
    "../lit:bundle",
    "../visual_logging:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "legacy.ts"

  deps = [
    ":css_files",
    ":ui",
  ]

  visibility = [
    # IMPORTANT: Be careful when extending visibility of front_end/ui/legacy,
    # as the UI module should only be used to actually build user interfaces,
    # and the business logic should never depend on the UI module.
    "../../entrypoints/*",
    "../../legacy_test_runner/*",
    "../../panels/*",
    "../../testing/*",
    "../components/adorners:*",
    "../components/data_grid:*",
    "../components/dialogs:*",
    "../components/docs/combo_box/*",
    "../components/docs/context_menu/*",
    "../components/docs/performance_panel/*",
    "../components/docs/radio_button/*",
    "../components/docs/slider/*",
    "../components/issue_counter:unittests",
    "../components/legacy_wrapper:*",
    "../components/linear_memory_inspector:*",
    "../components/list:*",
    "../components/markdown_view:*",
    "../components/menus:*",
    "../components/request_link_icon:unittests",
    "../components/snackbars:*",
    "../components/tooltips:*",
    "./*",

    # Required for x-link
    "../components/panel_feedback/*",
    "../components/settings/*",
    "../components/text_editor/*",

    # Required for correct button styling in the Computed tab
    # (http://crbug.com/435601104).
    "../components/tree_outline:*",
  ]

  visibility += devtools_ui_legacy_visibility
}

devtools_ui_module("unittests") {
  testonly = true

  sources = [
    "ARIAUtils.test.ts",
    "ActionRegistration.test.ts",
    "Context.test.ts",
    "ContextMenu.test.ts",
    "DOMUtilities.test.ts",
    "Dialog.test.ts",
    "DockController.test.ts",
    "FilterBar.test.ts",
    "Fragment.test.ts",
    "Infobar.test.ts",
    "InspectorView.test.ts",
    "KeyboardShortcut.test.ts",
    "ListModel.test.ts",
    "ListWidget.test.ts",
    "SearchableView.test.ts",
    "ShortcutRegistry.test.ts",
    "SoftContextMenu.test.ts",
    "SoftDropDown.test.ts",
    "SplitWidget.test.ts",
    "SuggestBox.test.ts",
    "TabbedPane.test.ts",
    "TextPrompt.test.ts",
    "Toolbar.test.ts",
    "Treeoutline.test.ts",
    "UIUtils.test.ts",
    "View.test.ts",
    "ViewManager.test.ts",
    "ViewRegistration.test.ts",
    "Widget.test.ts",
    "XLink.test.ts",
  ]

  deps = [
    ":bundle",
    "../../core/common:bundle",
    "../../core/i18n:bundle",
    "../../core/platform:bundle",
    "../../core/root:bundle",
    "../../testing",
    "../lit:bundle",
    "./components/quick_open:bundle",
  ]
}

component_doc("docs") {
  sources = [
    "ContextMenu.docs.ts",
    "RadioButton.docs.ts",
    "SelectMenu.docs.ts",
    "Slider.docs.ts",
  ]
  deps = [
    ":bundle",
    "../lit:bundle",
    "../visual_logging:bundle",
  ]
}
