# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../../../scripts/build/ninja/devtools_module.gni")
import("../../../../../scripts/build/ninja/generate_css.gni")
import("../../../../../scripts/build/typescript/typescript.gni")
import("../../visibility.gni")

generate_css("css_files") {
  sources = [ "spectrum.css" ]
}

devtools_ui_module("color_picker") {
  sources = [
    "ColorFormatSpec.ts",
    "ContrastDetails.ts",
    "ContrastInfo.ts",
    "ContrastOverlay.ts",
    "FormatPickerContextMenu.ts",
    "Spectrum.ts",
  ]

  deps = [
    "../../../../core/common:bundle",
    "../../../../core/host:bundle",
    "../../../../core/i18n:bundle",
    "../../../../core/platform:bundle",
    "../../../../core/sdk:bundle",
    "../../../../ui/components/srgb_overlay:bundle",
    "../../../../ui/kit:bundle",
    "../../../../ui/legacy:bundle",
    "../../../../ui/visual_logging:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "color_picker.ts"

  deps = [
    ":color_picker",
    ":css_files",
  ]

  visibility = [
    # IMPORTANT: Be careful when extending visibility of front_end/ui/legacy/*,
    # as the UI modules should only be used to actually build user interfaces,
    # and the business logic should never depend on the UI modules.
    ":*",
    "../../../../panels/css_overview/*",
    "../../../../panels/elements/*",
    "../../../../panels/sources/*",
    "../../../components/text_editor/*",
  ]

  visibility += devtools_ui_legacy_visibility
}

devtools_ui_module("unittests") {
  testonly = true

  sources = [
    "ColorFormatSpec.test.ts",
    "Spectrum.test.ts",
  ]

  deps = [
    ":bundle",
    "../../../../testing",
    "../../../legacy:bundle",
  ]
}
