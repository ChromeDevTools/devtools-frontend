# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../../../scripts/build/ninja/devtools_module.gni")
import("../../../../../scripts/build/ninja/generate_css.gni")
import("../../../../../scripts/build/typescript/typescript.gni")
import("../../visibility.gni")

generate_css("css_files") {
  sources = [
    "customPreviewComponent.css",
    "objectPopover.css",
    "objectPropertiesSection.css",
    "objectValue.css",
  ]
}

devtools_ui_module("object_ui") {
  sources = [
    "CustomPreviewComponent.ts",
    "JavaScriptREPL.ts",
    "ObjectPopoverHelper.ts",
    "ObjectPropertiesSection.ts",
    "RemoteObjectPreviewFormatter.ts",
  ]

  deps = [
    "../../../../core/common:bundle",
    "../../../../core/host:bundle",
    "../../../../core/i18n:bundle",
    "../../../../core/platform:bundle",
    "../../../../core/root:bundle",
    "../../../../core/sdk:bundle",
    "../../../../generated:protocol",
    "../../../../models/formatter:bundle",
    "../../../../models/geometry:bundle",
    "../../../../models/source_map_scopes:bundle",
    "../../../../models/text_utils:bundle",
    "../../../../third_party/acorn",
    "../../../../third_party/acorn:bundle",
    "../../../../ui/components/text_editor:bundle",
    "../../../../ui/i18n:bundle",
    "../../../../ui/kit:bundle",
    "../../../../ui/legacy:bundle",
    "../../../../ui/legacy/components/utils:bundle",
    "../../../components/highlighting:bundle",
    "../../../lit:bundle",
    "../../../visual_logging:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "object_ui.ts"

  deps = [
    ":css_files",
    ":object_ui",
  ]

  visibility = [
    # IMPORTANT: Be careful when extending visibility of front_end/ui/legacy/*,
    # as the UI modules should only be used to actually build user interfaces,
    # and the business logic should never depend on the UI modules.
    ":*",
    "../../../../panels/application/*",
    "../../../../panels/console/*",
    "../../../../panels/elements/*",
    "../../../../panels/event_listeners/*",
    "../../../../panels/linear_memory_inspector/*",
    "../../../../panels/network/*",
    "../../../../panels/profiler/*",
    "../../../../panels/sources/*",
    "../source_frame/*",
  ]

  visibility += devtools_ui_legacy_visibility
}

devtools_entrypoint("meta") {
  entrypoint = "object_ui-meta.ts"

  deps = [
    ":bundle",
    "../../../../core/sdk:bundle",
    "../../../../ui/legacy:bundle",
  ]

  visibility = [ "../../../../entrypoints/*" ]
}

devtools_ui_module("unittests") {
  testonly = true

  sources = [
    "JavaScriptREPL.test.ts",
    "ObjectPropertiesSection.test.ts",
  ]

  deps = [
    ":bundle",
    "../../../../testing",
    "../../../legacy:bundle",
  ]
}
