# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../../../scripts/build/ninja/devtools_entrypoint.gni")
import("../../../../../scripts/build/ninja/devtools_module.gni")
import("../../../../../scripts/build/ninja/generate_css.gni")
import("../../visibility.gni")

generate_css("css_files") {
  sources = [
    "imagePreview.css",
    "jsUtils.css",
  ]
}

devtools_module("utils") {
  sources = [
    "ImagePreview.ts",
    "JSPresentationUtils.ts",
    "Linkifier.ts",
    "Reload.ts",
    "TargetDetachedDialog.ts",
  ]

  deps = [
    "../../../../core/common:bundle",
    "../../../../core/host:bundle",
    "../../../../core/i18n:bundle",
    "../../../../core/platform:bundle",
    "../../../../core/sdk:bundle",
    "../../../../generated:protocol",
    "../../../../models/bindings:bundle",
    "../../../../models/text_utils:bundle",
    "../../../../models/workspace:bundle",
    "../../../../ui/legacy:bundle",
  ]
}

devtools_entrypoint("bundle") {
  entrypoint = "utils.ts"

  deps = [
    ":css_files",
    ":utils",
  ]

  visibility = [
    ":*",
    "../../../../../test/unittests/front_end/panels/console/*",
    "../../../../../test/unittests/front_end/ui/legacy/components/utils/*",
    "../../../../entrypoints/*",
    "../../../../panels/*",
    "../../../../panels/search/*",
    "../inline_editor/*",
    "../object_ui/*",

    # TODO(crbug.com/1202788): Remove invalid dependents
    "../../../../models/extensions/*",
    "../../../../models/persistence/*",
    "../../../components/linear_memory_inspector/*",
  ]

  visibility += devtools_ui_legacy_visibility
}

devtools_entrypoint("legacy") {
  entrypoint = "utils-legacy.ts"

  deps = [ ":bundle" ]

  visibility = [
    "../../../..:legacy_entrypoints",
    "../../../../legacy_test_runner/*",
  ]
}
